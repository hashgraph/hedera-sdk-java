plugins {
    id "java-library"
    id "idea"
    id "maven"
    id "com.bmuschko.nexus" version "2.3.1"
    id "com.google.protobuf" version "0.8.14"
}

group = "com.hedera.hashgraph"
version = "2.0.11"
description = "Hedera™ Hashgraph SDK for Java"

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    // https://github.com/bsideup/jabel
    annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.3.0'

    // custom annotation processor to help generate methods around executables
    annotationProcessor project(":executable-processor")
    compileOnly project(":executable-annotation")

	implementation "org.bouncycastle:bcprov-jdk15on:1.68"

    // Protobuf Lite is used to maintain easy compatibility with Android
    // https://github.com/protocolbuffers/protobuf/blob/master/java/lite.md
    api "com.google.protobuf:protobuf-javalite:3.14.0"

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation 'org.slf4j:slf4j-api:1.7.30'

	implementation "net.javacrumbs.future-converter:future-converter-java8-guava:1.2.0"

	implementation "io.grpc:grpc-core:1.38.0"
	implementation "io.grpc:grpc-protobuf-lite:1.38.0"
	implementation "io.grpc:grpc-stub:1.38.0"

    testCompile "org.assertj:assertj-core:3.20.2"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.6.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.6.1"

    testRuntimeOnly "org.slf4j:slf4j-simple:1.7.30"
    testRuntimeOnly "io.grpc:grpc-netty-shaded:1.38.0"
    integrationTestRuntimeOnly "io.grpc:grpc-netty-shaded:1.38.0"

	implementation "com.google.code.gson:gson:2.8.6"

    testImplementation "io.github.json-snapshot:json-snapshot:1.0.17"

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

// https://github.com/google/protobuf-gradle-plugin
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.14.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.38.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }

            task.plugins {
                grpc {
                    option "lite"
                }
            }
        }
    }
}


tasks.withType(JavaCompile).configureEach {
	// https://github.com/gradle/gradle/issues/2510#issuecomment-604986414
	// release = 9
	compileJava {
		options.compilerArgs += [
			"--release",
			"9" // Avoid using Java 12 APIs
		]
	}
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
        }
    }
    integrationTest {
        java.srcDir "$projectDir/src/integrationTest/java"

        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

idea {
    module {
        sourceDirs -= file("src/integrationTest/java")
        testSourceDirs += file("src/integrationTest/java")
    }
}

jacocoTestReport {
    // make sure to use any/all test coverage data for the report
    executionData fileTree(dir: buildDir, include: "jacoco/*.exec")

    // remove generated proto files from report
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/proto/**')
        }))
    }

    // configure it so only xml is generated for the report
    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        html.enabled true
        csv.enabled false
    }

    // make sure we run all tests before this report is made
    dependsOn tasks.withType(Test)
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.withType(Javadoc) {
    source = sourceSets.main.allJava + file("$buildDir/generated/sources/annotationProcessor/java/main")
}

tasks.withType(Test) {
    useJUnitPlatform()

    // NOTE: Uncomment to enable trace logs in the SDK during tests
    // jvmArgs "-Dorg.slf4j.simpleLogger.log.com.hedera.hashgraph=trace"

    // this task will fail on the first failed test
    failFast = true

    // emit logs per passed or failed test
    testLogging {
        exceptionFormat = 'full'
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }

    // propagate system environment to test runner
    systemProperty "OPERATOR_ID", findProperty("OPERATOR_ID")
    systemProperty "OPERATOR_KEY", findProperty("OPERATOR_KEY")
    systemProperty "CONFIG_FILE", findProperty("CONFIG_FILE")
    systemProperty "HEDERA_NETWORK", findProperty("HEDERA_NETWORK")
}

modifyPom {
	project {
		description 'Hedera™ Hashgraph SDK for Java'
		name 'sdk'

		url 'https://github.com/hashgraph/hedera-sdk-java'

		organization {
			name 'Hedera Hashgraph'
			url 'https://www.hedera.com'
		}

		issueManagement {
			system 'GitHub'
			url 'https://github.com/hashgraph/hedera-sdk-java/issues'
		}

		licenses {
			license {
				name 'Apache License, Version 2.0'
				url 'https://github.com/hashgraph/hedera-sdk-java/blob/master/LICENSE'
				distribution 'repo'
			}
		}

		scm {
			url 'https://github.com/hashgraph/hedera-sdk-java'
			connection 'scm:git:https://github.com/hashgraph/hedera-sdk-java.git'
			developerConnection 'scm:git:ssh://github.com:hashgraph/hedera-sdk-java.git'
		}

		developers {
			developer {
				name 'Ryan Leckey'
			}
		}
	}
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
}

jar {
	exclude "**/*.proto"
}

tasks.withType(Jar.class) {
	archiveBaseName = "sdk"
	includeEmptyDirs = false
}
