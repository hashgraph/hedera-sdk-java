plugins {
    id "com.android.application"
    id "kotlin-android"
    id "com.quittle.android-emulator"
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.hedera.android_example"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

	namespace 'com.hedera.android_example'
}

androidEmulator {
    def arch = System.getProperty("os.arch")
    def abiVersion = arch.contains('arm64') || arch.contains("aarch64") ? 'arm64-v8a' : 'x86_64'
    emulator {
        name "emulator-5554"
        sdkVersion 33
        device 'pixel_3a'
        abi abiVersion
        includeGoogleApis true
    }
    enableForAndroidTests false
    headless false
    logEmulatorOutput true
    additionalEmulatorArguments '-no-snapshot'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation "androidx.core:core-ktx:1.9.0"
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
	implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'com.hedera.hashgraph:sdk:2.17.4'

    implementation 'org.slf4j:slf4j-api:2.0.3'
    implementation 'io.grpc:grpc-okhttp:1.46.0'
}

repositories {
    mavenLocal()
	mavenCentral()
}

tasks.create("run") {
    group 'application'
    afterEvaluate {
        dependsOn tasks.waitForAndroidEmulator
        dependsOn tasks.installDebug

        tasks.installDebug.configure { mustRunAfter tasks.waitForAndroidEmulator }
        tasks.stopAndroidEmulator.configure { enabled false }
    }
}
